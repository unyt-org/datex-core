WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

// operations
factor_operator = { "*" | "/" | "%" }
sum_operator = { "+" | "-" | "." }

factor_expression = { _atom ~ (factor_operator ~ _atom)+ }
expression  = { (factor_expression | _atom) ~ (sum_operator ~ (factor_expression | _atom))+ | factor_expression | _atom }

params = !{ "(" ~ statements ~ ")" }
apply = ${atom_with_parens ~ (WHITESPACE+ ~ atom | WHITESPACE* ~ params)+}

_atom = _{ apply | atom_with_parens }

atom = !{
	object |
	array |
	string |
	decimal |
    integer |
	literals |
    placeholders |
    variable
}

atom_with_parens = _{
    "(" ~ statements? ~ ")" |
	atom
}

placeholders = _{ placeholder_with_index | placeholder }

placeholder_with_index = { "?" ~ ASCII_DIGIT+ }
placeholder = { "?" }

_literals = _{ boolean | null | void }

literals = _{ _literals ~ !ASCII_ALPHANUMERIC }

boolean = { "true" | "false" }

null = { "null" }
void = { "void" }

object = {
    "{" ~ "}" |
    "{" ~ pair ~ ("," ~ pair)* ~ "}"
}
pair = { string ~ ":" ~ atom_with_parens }

array = {
    "[" ~ "]" |
    "[" ~ atom_with_parens ~ ("," ~ atom_with_parens)* ~ "]"
}

integer = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
}

decimal = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}


string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
    | "\n"
}

variable = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }


statements = _{ expression ~ (";"+ ~ expression)* ~ (";")* }

datex = _{ SOI ~ statements ~ EOI }